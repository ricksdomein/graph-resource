#!/usr/bin/env -S gnuplot --persist -c
# Plot memory and CPU usage over time. Usage:
# usage-plot.gp <input file> [<output .png file>]
# where the input file has the columns `<unix time> <% cpu> <% memory>`


set term png small size 800,600
set output ARG2

set xdata time
set timefmt "%s"
set format x "%d/%m/%Y %H:%M:%S"
set xlabel "Time [[hh:]mm:ss]"
set xtics rotate 60

set ylabel "%CPU"
set y2label "%MEM"

set ytics nomirror
set y2tics nomirror in

set yrange [0:*]
set y2range [0:*]


set style line 1 linecolor 'blue'
set style line 2 linecolor 'light-green'
set grid
set key opaque
set border back

plot ARG1 using 1:2 with lines axes x1y1 title "%CPU" linestyle 1, \
     ARG1 using 1:3 with lines axes x1y2 title "%MEM" linestyle 2
